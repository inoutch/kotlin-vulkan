plugins {
    id "kotlin-multiplatform"
}

def extraJvmArgs = ""
def mainClassName = "io.github.inoutch.kotlin.vulkan.example.MainKt"

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.35.0"
}

// jvm
def platform = org.gradle.internal.os.OperatingSystem.current()
switch (platform) {
    case org.gradle.internal.os.OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case org.gradle.internal.os.OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case org.gradle.internal.os.OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        extraJvmArgs = ["-XstartOnFirstThread"]
        break
}

kotlin {
    jvm {}
    iosArm64 {
        binaries.executable {
            entryPoint = "io.github.inoutch.kotlin.vulkan.example.main"
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion"

                implementation project(":kotlin-vulkan")
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlinVersion"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlinVersion"
            }
        }
        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"

                implementation project(":kotlin-vulkan")

                implementation "org.lwjgl:lwjgl:$lwjglVersion"
                implementation "org.lwjgl:lwjgl-stb:$lwjglVersion"
                implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
                implementation "org.lwjgl:lwjgl-vulkan:$lwjglVersion"

                runtimeOnly "org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives"
                runtimeOnly "org.lwjgl:lwjgl-stb:$lwjglVersion:$lwjglNatives"
                runtimeOnly "org.lwjgl:lwjgl-glfw:$lwjglVersion:$lwjglNatives"
                if (platform == org.gradle.internal.os.OperatingSystem.MAC_OS) {
                    runtimeOnly "org.lwjgl:lwjgl-vulkan:$lwjglVersion:$lwjglNatives"
                }
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
                implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
            }
        }
        iosArm64Main {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion"

                implementation project(":kotlin-vulkan")
            }
        }
    }
}

// extra tasks
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

task runJvm(type: JavaExec) {
    main = mainClassName
    def target = kotlin.targets.jvm
    def compilation = target.compilations.main
    def classes = files(
            compilation.runtimeDependencyFiles,
            compilation.output.allOutputs
    )
    classpath = classes
    jvmArgs extraJvmArgs
}

task buildAppForXcode {
    if (isCalledFromXcode()) {
        dependsOn kotlin.targets.iosArm64.binaries.getExecutable(getBuildTypeForXcode()).linkTask
    }

    doLast {
        if (!isCalledFromXcode()) {
            throw new Exception("Please run 'buildAppForXcode' task with all necessary properties!")
        }

        copy {
            from file(kotlin.targets.iosArm64.binaries.getExecutable(getBuildTypeForXcode()).outputFile)
            into file(getBinaryLocationForXcode().parentFile)
            rename {
                getBinaryLocationForXcode().name
            }
        }
    }
}

task compileShaders {
    def shaderDir = "src/shaders"
    def buildShaderDir = "${buildDir}/shaders"
    def names = ["triangle", "tutorialDepthBuffering"]
    mkdir(buildShaderDir)
    doLast {
        names.forEach {
            def name = it
            exec {
                workingDir shaderDir
                commandLine "glslangValidator", "-e", "main", "-V", "-o", "${buildShaderDir}/${name}.vert.spv", "${name}.vert"
            }
            exec {
                workingDir shaderDir
                commandLine "glslangValidator", "-e", "main", "-V", "-o", "${buildShaderDir}/${name}.frag.spv", "${name}.frag"
            }
            exec {
                workingDir buildShaderDir
                commandLine "hexdump", "-e", '16/1 "0x%02x," "\\n"', "${name}.vert.spv"
                standardOutput new FileOutputStream("${buildShaderDir}/${name}.vert.hex")
            }
            exec {
                workingDir buildShaderDir
                commandLine "hexdump", "-e", '16/1 "0x%02x," "\\n"', "${name}.frag.spv"
                standardOutput new FileOutputStream("${buildShaderDir}/${name}.frag.hex")
            }
        }
    }
}

private boolean isCalledFromXcode() {
    project.hasProperty("configuration.name") && project.hasProperty("binary.location")
}

private String getBuildTypeForXcode() {
    project.properties["configuration.name"] as String
}

private File getBinaryLocationForXcode() {
    file(project.properties["binary.location"])
}
